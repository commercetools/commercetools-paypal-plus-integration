buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.3.RELEASE")
    }
}

repositories {
    jcenter()
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

targetCompatibility = 1.8
sourceCompatibility = 1.8


jar {
    version = '0.1.0-DEV'
    archiveName = "${rootProject.name}.jar"
    manifest {
        attributes("Implementation-Title": rootProject.name,
                "Implementation-Version": version)
    }
}

final depVersions = [
        springBoot         : '1.5.4.RELEASE',
        commercetoolsSdkJvm: '1.20.0',
        paypalPlusSdk      : '1.13.1',
        assertJ            : '3.8.0'
]

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:${depVersions.springBoot}")

    compile "com.paypal.sdk:rest-api-sdk:${depVersions.paypalPlusSdk}"

    compile "com.commercetools.sdk.jvm.core:commercetools-models:${depVersions.commercetoolsSdkJvm}"
    compile "com.commercetools.sdk.jvm.core:commercetools-java-client:${depVersions.commercetoolsSdkJvm}"

    testCompile("org.springframework.boot:spring-boot-starter-test:${depVersions.springBoot}") {
        exclude group: 'org.assertj', module: 'assertj-core' // below override the default outdated one with the latest
    }

    testCompile "org.assertj:assertj-core:${depVersions.assertJ}"
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.47'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

test {
    testLogging {
        // set options for log level LIFECYCLE
        events "passed", "skipped", "failed"
        displayGranularity 2
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true

        maxGranularity 3

        minGranularity 0

    }
}
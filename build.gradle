buildscript {

    // we need to share Spring boot version across buildscript and the rest of the application. Unfortunately, this
    // is only the one known way:
    // https://discuss.gradle.org/t/how-to-access-project-ext-variables-from-within-buildscript-section/5706/3
    ext.springBoot = '1.5.9.RELEASE'
    ext.grgitVersion = '2.0.1'
    ext.testLoggerVersion = '1.1.2'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBoot")
        classpath("org.ajoberstar:grgit:$grgitVersion")
        classpath("com.adarshr:gradle-test-logger-plugin:$testLoggerVersion")
    }
}

repositories {
    jcenter()
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'com.adarshr.test-logger'

targetCompatibility = 1.8
sourceCompatibility = 1.8

version = '0.3.0-DEV' // this will be replaced by git tag version, if available

ext {
    // try to define git tag version and replace project.version with this value, if exists.
    // otherwise - concat commit number to project.version
    final HEAD = grgit.head()

    final tags = grgit.tag.list()
    final currentCommitTag = tags.find({ it.commit == HEAD })
    project.version = currentCommitTag ? currentCommitTag.name : "${project.version}-${HEAD.abbreviatedId}"
    println "Build project version: ${project.version}"
}

jar {
    version = project.version
    archiveName = "${rootProject.name}.jar"
    manifest {
        attributes("Implementation-Title": rootProject.name,
                "Implementation-Version": version)
    }
}

final depVersions = [
        springBoot         : ext.springBoot,
        commercetoolsSdkJvm: '1.27.0',
        paypalPlusSdk      : '1.14.0',
        guava              : '23.0',
        apacheCollections  : '4.0',

        // tests
        assertJ            : '3.8.0',
        systemRules        : '1.17.0'
]

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:${depVersions.springBoot}"

    compile "com.paypal.sdk:rest-api-sdk:${depVersions.paypalPlusSdk}"

    compile "com.commercetools.sdk.jvm.core:commercetools-models:${depVersions.commercetoolsSdkJvm}"
    compile "com.commercetools.sdk.jvm.core:commercetools-java-client:${depVersions.commercetoolsSdkJvm}"
    compile "com.commercetools.sdk.jvm.core:commercetools-convenience:${depVersions.commercetoolsSdkJvm}"
    compile "com.google.guava:guava:${depVersions.guava}"
    compile "org.apache.commons:commons-collections4:${depVersions.apacheCollections}"


    testCompile("org.springframework.boot:spring-boot-starter-test:${depVersions.springBoot}") {
        exclude group: 'org.assertj', module: 'assertj-core' // below override the default outdated one with the latest
    }

    testCompile "org.assertj:assertj-core:${depVersions.assertJ}"
    testCompile "com.github.stefanbirkner:system-rules:${depVersions.systemRules}"

}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

testlogger {
    theme 'mocha'
    slowThreshold 20000
}
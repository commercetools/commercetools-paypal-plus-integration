// unit/integration tests configurations
//
// https://www.petrikainulainen.net/programming/gradle/getting-started-with-gradle-integration-testing/
// https://www.michael-bull.com/blog/2016/06/04/separating-integration-and-unit-tests-with-gradle
// https://discuss.gradle.org/t/separate-execution-for-java-unit-and-integration-tests/8713

final testsRootDir = 'src/test'
final testsCommonsDir = "${testsRootDir}/common"
final testsUnitDir = "${testsRootDir}/unit"
final testsIntegrationDir = "${testsRootDir}/integration"
final testsUsecasesDir = "${testsRootDir}/usecases"

sourceSets {
    testCommon {
        java {
            srcDirs = ["$testsCommonsDir/java"]
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
        resources.srcDirs = ["$testsCommonsDir/resources"]
    }

    test {
        java {
            srcDirs = ["$testsUnitDir/java"]
            compileClasspath += main.output + testCommon.output
            runtimeClasspath += main.output + testCommon.output
        }
        resources.srcDirs = ["$testsUnitDir/resources"]
    }

    testIntegration {
        java {
            srcDirs = ["$testsIntegrationDir/java"]
            compileClasspath += main.output + testCommon.output
            runtimeClasspath += main.output + testCommon.output
        }
        resources.srcDirs = ["$testsIntegrationDir/resources"]
    }

    testUsecases {
        java {
            srcDirs = ["$testsUsecasesDir/java"]
            compileClasspath += main.output + testCommon.output
            runtimeClasspath += main.output + testCommon.output
        }
        resources.srcDirs = ["$testsUsecasesDir/resources"]
    }
}

configurations {
    testCommonCompile.extendsFrom compile
    testCommonRuntime.extendsFrom runtime

    testCompile.extendsFrom testCommonCompile
    testRuntime.extendsFrom testCommonRuntime

    testIntegrationCompile.extendsFrom testCommonCompile
    testIntegrationRuntime.extendsFrom testCommonRuntime

    testUsecasesCompile.extendsFrom testCommonCompile
    testUsecasesRuntime.extendsFrom testCommonRuntime
}

task testIntegration(type: Test) {
    description "Run only the integration tests from ${sourceSets.testIntegration.java.srcDirs}."
    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
    outputs.upToDateWhen { false }
}

task testUsecases(type: Test) {
    description 'More complex than just Integration Tests: ' +
            'these cases are executed over running local REST service (docker containers).' +
            "The test sources are located in ${sourceSets.testUsecases.java.srcDirs}"

    testClassesDirs = sourceSets.testUsecases.output.classesDirs
    classpath = sourceSets.testUsecases.runtimeClasspath
    outputs.upToDateWhen { false }
}

task testUnit(dependsOn: test) {
    description "Run only the unit tests from ${sourceSets.test.java.srcDirs}. This is an alias for :test task"
}
